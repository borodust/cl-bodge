(cl:in-package :cl-bodge.resources)


;;;
;;; BRF container node
;;;

(defclass container-node (path-node)
  ((container)
   (root-path :initarg :root-path :initform #p"/")))


(defmethod initialize-instance :after ((this container-node) &key container-path)
  (with-slots (container) this
    (setf container (load-container container-path))))


(defun open-bound-chunk-stream (container path)
  (when-let ((record (find-chunk container (namestring path))))
    (let ((stream (open (path-of container) :element-type '(unsigned-byte 8))))
      (file-position stream (chunk-record-position record))
      (make-bounded-input-stream stream (chunk-record-size record)))))


(defmethod open-resource-stream ((this container-node) (path null))
  (with-slots (root-path container) this
    (open-bound-chunk-stream container root-path)))


(defmethod open-resource-stream ((this container-node) (path cons))
  (with-slots (container root-path) this
    (if-let ((stream (call-next-method)))
      stream
      (let ((name (fad:merge-pathnames-as-file root-path (format nil "窿疳翳┅┅镳孱怙躅洵汨躅氕篝蝈犴泔铘衢铄钺礤┅┅ㄤ彐躅磲脲泔铘衢铄颦蝈箫躜沐痱秭殇弪ㄣ镱翎轭弪疳翳镳糸镱犰蝻雉疳翳┅灬礅溽铒溴钺礤磲脲轭篝犷沐с镱翎轭弪铒溴侯犴铒溴钺礤恒镱翎轭弪疳翳泔铘衢铄颦疳翳候镲舡疳翳蝻雉疳翳┅ㄤ彐躅盹躅舡泔铘衢铄蝈箫躜沐疳翳泔铘衢铄颦疳翳镳糸镱犰ㄣ镱翎轭弪蝻雉┅盹躅舡蝈箫躜沐痱秭殇弪蝈箫躜沐疳翳磲脲泔铘衢铄颦蝈箫躜沐痱秭殇弪泔铘衢铄颦疳翳泔铘衢铄颦蝻雉┅